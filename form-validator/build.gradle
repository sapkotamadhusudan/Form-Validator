plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.jfrog.bintray' version "1.8.1"
    id 'maven-publish'
}

def getVersionName = { ->
    return "0.0.1"
}

def getArtifactId = { ->
    return "form-validator"
}

def getGroupId = { ->
    return "com.maddy"
}

def getDescription = { ->
    return "A Toolkit to maintain and validates form fields in android application."
}

def getVCSUrl = { ->
    return "https://github.com/sapkotamadhusudan/form-validator"
}

def getLicense = { ->
    return ["MIT"]
}

def getPackageName = { ->
    return getGroupId() + ":" + getArtifactId()
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId getGroupId()
                artifactId getArtifactId()
                version getVersionName()
                // Place the path of your artifact here
                artifact("$buildDir/outputs/aar/${getArtifactId()}-release.aar")
            }
        }
    }
}

// Bintray config
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ["release"]
    pkg {
        repo = "form-validator"
        name = getPackageName()
        desc = getDescription()
        licenses = getLicense()
        vcsUrl = getVCSUrl()
        version {
            name = getVersionName()
            vcsTag = getVersionName()
            released = new Date()
        }
    }
}


android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}